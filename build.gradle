/*
  Gradle file to build and run a sample spring boot application.
*/
plugins {
  id 'java'
  id 'eclipse'
  id 'idea'
  id 'org.springframework.boot' version '2.1.0.RELEASE'
  id "in.co.rahogata.swayamjar" version "0.2"
}

apply plugin: 'io.spring.dependency-management'

version = '0.1'

ext {
  junitVersion = '5.3.2'
}

final modsToExclude = [ 'junit' ]

configurations {
  modsToExclude.each { implementation.exclude module : it } 
}

repositories {
  mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"

    testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
  useJUnitPlatform()
}

task cleanFb(type: Delete) {
	delete 'src/main/resources/public'
}

task copyFrontEnd(type: Copy, dependsOn: cleanFb) {
	from 'india-flag/build'
	into 'src/main/resources/public'
}

bootJar {
	dependsOn copyFrontEnd
}

swayamJar {
	dependencies bootJar
	source = bootJar.outputs.files.singleFile
}

assemble.dependsOn tasks.swayamJar
